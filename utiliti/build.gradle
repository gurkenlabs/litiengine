import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
  id "java"
  id "application"

  alias libs.plugins.beryxRuntime

  id "encoding-defaults"
  id "licensing-defaults"
  id "testing-defaults"
}

sourceSets {
  main {
    resources {
      srcDir("src/main/localization")
    }
  }
}

application {
  mainClass = "de.gurkenlabs.utiliti.Program"
  applicationName = utiLITI_name
  description = utiLITI_description
  applicationDistribution.into(executableDir) {
    from("src/dist")
  }
}

dependencies {
  implementation project(":litiengine")
  implementation libs.darklaf.core
  implementation libs.darklaf.propertyLoader
  testImplementation project(":shared")
  testImplementation libs.junit.jupiter
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

runtime {
  options.set(["--strip-debug", "--no-header-files", "--no-man-pages"])
  modules.set(["java.desktop", "java.logging", "java.datatransfer", "java.management", "java.xml"])

  launcher {
    noConsole = false
  }

  jpackage {
    appVersion = project.version.toString().tokenize("-")[0]
    skipInstaller = true
    var currentOs = DefaultNativePlatform.currentOperatingSystem
    var iconFileType = currentOs.isWindows() ? "ico" : currentOs.isMacOsX() ? "icns" : "png"

    imageOptions.addAll([
      "--icon", "${project.file("src/dist/pixel-icon-utiliti.$iconFileType").path}",
      "--description", description,
      "--copyright", copyright,
      "--vendor", "gurkenlabs.de",
      "--java-options", "-Xms256m",
      "--java-options", "-Xmx2048m",
      "--java-options", "--add-exports java.desktop/sun.awt=ALL-UNNAMED"
    ] as Collection<? extends String>)

    if (currentOs.isMacOsX()) {
//      TODO: Once we are above version 1.0.0, we should be able to use the project version directly for Mac builds.
      def tmpAppVersion = project.version.toString().tokenize("-")[0].tokenize(".")
      tmpAppVersion[0] = "1337"
      appVersion = tmpAppVersion.join('.')
      imageOptions.addAll(
        ["--mac-package-identifier", "de.gurkenlabs.litiengine.utiliti",
         "--mac-package-name", applicationName // Name appearing in Menu Bar.
        ]
      )
    }
  }
}

tasks.register("createLicenseProperties", WriteProperties) {
  dependsOn processResources
  destinationFile = file("${layout.buildDirectory.get()}/resources/main/licensing.properties")
  encoding = "UTF-8"
  property('copyright', copyright.toString())
  property('website', gurkenlabs_url)
  property('version', version.toString())
}

classes {
  dependsOn createLicenseProperties
}
